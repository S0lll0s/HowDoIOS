; m35fd_block_io.dasm: Mackapar M35FD BLOCK_IO Driver
;
; This file is part of HowDoIOS.
; Visit our git repo at https://github.com/S0lll0s/HowDoIOS
;  Copyright (C) 2013 S0lll0s

; BLOCK_IO - Block-based I/O (eg. for M35FD)

.reserve 0x20 ; prevent short literals in our labels

#define BLOCK_IO					2

:header_start
	dat 0x4110						; magic number
	dat 0x0100						; BI version 1.0
	dat (code-header_start)			; header size
	dat (end-code)					; code & data size
	dat	1							; flags (singleton)
	dat	0xffff						; entry point = none
	; relocation table
	dat 7
		dat r1+1, r2+1, r3+1, r4+1, r5+1, r6+1, r7+1
	; exports table
	dat 1			; 1 entry
		dat BLOCK_IO	; ifd: BLOCK_IO
		dat 5			; 5 export entries
			dat (_init-code)	; init()
			dat (_exit-code)	; exit()
			dat 0x200			; blocksize
			dat 0x5A0			; num blocks
			dat (_load-code)	; load( id, dest )
			dat (_store-code)	; store( src, id )
	; imports table
	dat 1			; 1 entry
		dat HARDWARE	; ifd: HARDWARE
:code
:_init
	SET A, 0
:r1	SET B, hardware_imports-code	; destination
	INT DEP_LOAD
	IFN A, 0
:r2		SET [set_up-code], 1
	SET PC, POP
:_exit
:r3	SET [set_up-code], 0
	SET PC, POP
:_load
	SET PUSH, X
	SET PUSH, Y
	SET A, 1
	SET X, B
	SET Y, C
	HWI HARDWARE_ID
:r4	SET PC, wait_ready-code
:_store
	SET PUSH, X
	SET PUSH, Y
	SET A, 2
	SET X, C
	SET Y, B
	HWI HARDWARE_ID
:r5	SET PC, wait_ready-code
	
:wait_ready
	SET A, 1
	dat "B"
:r6	SET PC, f-code
:loop
	INT PROC_SKIP
:f	HWI HARDWARE_ID
	IFE B, 3
:r7	SET PC, loop-code
	SET Y, POP
	SET X, POP
	SET PC, POP

:set_up dat 0

#include '../interfaces/hardware.dasm'
:end

#include '../api.dasm'