; utils.dasm: utility syscalls
;
; This file is part of HowDoIOS.
; Visit our git repo at https://github.com/S0lll0s/HowDoIOS
;  Copyright (C) 2013 S0lll0s

; MEMSET (0xf030)
; sets memory between (including) A and B to C
:_memset
	SET A, PEEK ; leave A on the stack
	JSR __memset
	RFI 0

:__memset
:.loop
	IFG A, B
		SET PC, POP
	SET [A], C
	ADD A, 1
	SET PC, .loop

; MEMCPY (0xf031)
; copies memory between (including) A and B to memory starting at C
:_memcpy
	SET A, PEEK
	JSR __memcpy
	RFI 0

:__memcpy
:.loop
	IFG A, B
		SET PC, POP
	SET [A], [C]
	ADD A, 1
	ADD C, 1
	SET PC, .loop

; STRCMP (0xf032)
; compares two strings, returns 1 on success, B is the last character
:_strcmp
	SET A, PEEK
	JSR __strcmp
	SET PEEK, A
	RFI 0

:__strcmp
	SET C, 0
:.loop
	IFE [A], 0
		IFE [B], 0
			SET PC, .success
	IFN [A], [B]
		SET PC, .wind
	ADD A, 1
	ADD B, 1
	ADD C, 1
	SET PC, .loop
:.wind
	IFE [B], 0
		SET PC, .failure
	ADD B, 1
	ADD C, 1
	SET PC, .wind
:.failure
	SET A, 0
	SET PC, POP
:.success
	SET A, 1
	SET PC, POP